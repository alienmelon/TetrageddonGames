import mx.utils.Delegate;
/**
* Spews forth a short stream of random junk.
* @version 1.0.0
* @since 0.0.9
*/
class tetrigeddon.display.Debris
{
	public var symbol:String;
	private var intervals:Array;
	
	public function Debris()
	{
		symbol = "";
		intervals = new Array();
	}
	
	public function spawn(x:Number, y:Number, amount:Number, s:String):Void
	{
		var i:Number = 0;
		if (amount == undefined) amount = 5;
		if (s == undefined) s = this.symbol;
		while (++i < amount)
		{
			var debris:MovieClip = _root.attachMovie(s, "debris_" + Game.randomNumber(0, 600), _root.getNextHighestDepth());
			debris.cacheAsBitmap = true;
			debris.stop();
			debris._x = x;
			debris._y = y;
			debris.xMov = Game.randomNumber(-10, 10);
			debris.yMov = Game.randomNumber(-10, -20);
//			debris.rotateNum = Game.randomNumber(-5, 5);
			debris.floor = Game.randomNumber(570, 570 + (Math.floor(Math.random() * 5)));
			debris.iIndex = intervals.length;
			debris.counter = 100;
			var index:Number = intervals.push(setInterval(this, "animate", 20, debris)) - 1;
		}
	}
	
	private function animate(mc:MovieClip):Void
	{
		if (mc.counter <= 0)
		{
//			trace("<<Debris::animate>> Stopping gib " + intervals[mc.iIndex]);
			clearInterval(intervals[mc.iIndex]);
			mc.removeMovieClip();
		}
		if (mc._y >= mc.floor)
		{
			mc.gotoAndStop("collide");
			mc.counter -= 5;
			return;
		}
		mc._x += mc.xMov;
		mc._y += mc.yMov;
		mc.xMov *= .99;
		mc.yMov *= .99;
		mc.yMov += 3;
//		mc._rotation += mc.rotateNum;
	}
}
